plugins {
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'org.asciidoctor.jvm.convert' version "${asciidoctorVersion}"
	id 'org.asciidoctor.jvm.pdf' version "${asciidoctorVersion}"
	id 'org.asciidoctor.jvm.gems' version "${asciidoctorVersion}"
	id("org.asciidoctor.base") version "${asciidoctorVersion}"
	id "com.bmuschko.docker-remote-api" version "${dockerRemoteApiVersion}"
//	id "com.bmuschko.docker-java-application" version "${dockerRemoteApiVersion}"
	id 'java'
	id 'war'
	id 'jacoco'
	id 'idea'
}
//
//apply from: 'gradle/integration-test.gradle'

group = 'com.walshdanny700'
version = '0.0.1-SNAPSHOT'

tasks.named('wrapper'){
	distributionType = Wrapper.DistributionType.ALL
	gradleVersion = '7.4.2'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

ext {
	snippetsDir = file('build/generated-snippets')
}

dependencies {

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-data-jpa
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${springBootStarterVersion}"
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootStarterVersion}"

	// https://mvnrepository.com/artifact/org.flywaydb/flyway-core
	implementation group: 'org.flywaydb', name: 'flyway-core', version: "${flywayCoreVersion}"

	testImplementation group: "org.junit.jupiter", name : "junit-jupiter-api", version: "${jupiterVersion}"
	testRuntimeOnly group: "org.junit.jupiter", name :"junit-jupiter-engine", version: "${jupiterVersion}"

	// https://mvnrepository.com/artifact/org.projectlombok/lombok
	compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"

    // https://mvnrepository.com/artifact/com.h2database/h2
	runtimeOnly group: 'com.h2database', name: 'h2', version: "${h2Version}"

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-tomcat
	providedRuntime group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: "${springBootStarterVersion}"
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
	testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBootStarterVersion}"

	//Rest Docs  https://mvnrepository.com/artifact/org.springframework.restdocs/spring-restdocs-mockmvc
	testImplementation group: "org.springframework.restdocs", name: "spring-restdocs-mockmvc", version: "${restdocsMockmvcVersion}"

	// https://mvnrepository.com/artifact/org.springframework.restdocs/spring-restdocs-webtestclient
	implementation group: 'org.springframework.restdocs', name: 'spring-restdocs-webtestclient', version:  "${restdocsMockmvcVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-test-autoconfigure
	testImplementation group: 'org.springframework.boot', name: 'spring-boot-test-autoconfigure', version: '2.6.7'



	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	// https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
	implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: "${httpClientVersion}"

//	// https://mvnrepository.com/artifact/org.springframework/spring-webflux
//	testImplementation group: 'org.springframework', name: 'spring-webflux', version: "${springWebfluxVersion}"

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-webflux
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: "${springBootStarterVersion}"

}


test {

	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}

	finalizedBy jacocoTestReport

	outputs.dir snippetsDir
}

jacoco {
	toolVersion = "0.8.7"
	reportsDirectory = file("$buildDir/customJacocoReportDir") as Directory
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report

	reports {
		xml.required = false
		csv.required = true
		html.required = true
		csv.outputLocation  = file("${buildDir}/reports/jacoco/test/jacocoTestReport.csv" )
		html.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoHtml" )
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.9
			}
		}

		rule {
			enabled = true
			element = 'CLASS'
			includes = ['com.walshdanny700.campaign_optimisation.*']

			limit {
				counter = 'LINE'
				value = 'TOTALCOUNT'
				maximum = 0.9
			}
		}
	}
}

asciidoctor {
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
//	outputDir "${buildDir}/docs/asciidoc"
	sourceDir 'src/docs/asciidoc'
	outputDir "build/asciidoc/html5"
	dependsOn test
}


war {
	enabled = false
	archiveClassifier=''

}

task customFatWar(type: War) {
	enabled = true
	archiveClassifier = '' //use empty string
	dependsOn asciidoctor
	from ("build/asciidoc/html5"){
		include '**/*.*'
	}

	manifest {
		attributes 'Main-Class': 'com.walshdanny700.campaign_optimisation.CampaignOptimisationApplication'
	}
	archiveBaseName = 'all-in-one-war'
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	with war


}


bootWar {
	dependsOn asciidoctor
	from ("build/asciidoc/html5"){
		into "static/docs"
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

if (hasProperty('buildScan')) {
	buildScan {
		termsOfServiceUrl = 'https://gradle.com/terms-of-service'
		termsOfServiceAgree = 'yes'
	}
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
	destFile = project.file('build/docker/Dockerfile')
	from 'openjdk:17-alpine'
	label(['maintainer': "Danny Walsh 'walshdanny700@gmail.com'"])
	copyFile("campaign_optimisation-${version}.war", 'app.war')
	entryPoint 'java'
	defaultCommand '-jar', 'app.war'
	exposePort 8080
}

task syncWebAppArchive(type: Sync) {
	dependsOn assemble
	from war.archiveFile
	into 'build/docker'
}

createDockerfile.dependsOn syncWebAppArchive

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task buildImage(type: DockerBuildImage) {
	dependsOn createDockerfile
}


bootBuildImage{
	dependsOn buildImage
	imageName = 'walshdanny700/campaign_optimisation:latest'
	publish = false
}

sourceSets {
	integrationTest {
		java.srcDir file('src/integrationTest/java')
		resources.srcDir file('src/integrationTest/resources')
		compileClasspath += sourceSets.main.compileClasspath + sourceSets.test.compileClasspath
		runtimeClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
	}
}

idea{
	module {
		sourceSets.integrationTest.allSource.srcDirs.each { srcDir -> module.testSourceDirs += srcDir }
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform()
	description = 'Runs the integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	reports {
		html.required = true
		html.outputLocation  = file("${buildDir}/reports/test/integration-test" )
	}

}
